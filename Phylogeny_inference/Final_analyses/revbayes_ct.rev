################################################################################
#
# RevBayes Exercise: Bayesian inference using MCMC and output analysis
# 
# This file: Specifies the full model parameters and moves for a 2-gene
#        partition model
#
# authors: Tracy A. Heath, Michael Landis, Sebastian Hoehna and Brian R. Moore
#
################################################################################


#######################
# Reading in the Data #
#######################

# read in the character data
filenames <- v("18sublist.aln.fasta", "16sublist.aln.fasta")

n_data_subsets <- filenames.size()
for (i in 1:n_data_subsets) {
    data[i] = readDiscreteCharacterData(filenames[i])
}

# Get some useful variables from the data. We need these later on.
taxa <- data[1].taxa()
n_species <- data[1].ntaxa()
n_branches <- 2 * n_species - 3

mvi = 0
mni = 0

##################################
#      Substitution Model        #
#   Loop over each data subset   #
##################################
  
for (i in 1:n_data_subsets) {

    # exchangeability rates for partition i
    er_prior[i] <- v(1,1,1,1,1,1)
    er[i] ~ dnDirichlet(er_prior[i])
    moves[++mvi] = mvSimplexElementScale(er[i], alpha=10, tune=true, weight=3) 

    # stationary frequencies for partition i
    pi_prior[i] <- v(1,1,1,1)
    pi[i] ~ dnDirichlet(pi_prior[i])
    moves[++mvi] = mvSimplexElementScale(pi[i], alpha=10, tune=true, weight=2) 

    # rate matrix for partition i
    Q[i] := fnGTR(er[i],pi[i]) 
  
    # +Gamma for partition i
    alpha_prior_mean[i] <- 5.0
    alpha_prior_sd[i] <- 0.587405
    alpha[i] ~ dnLognormal( alpha_prior_mean[i], alpha_prior_sd[i] )
    gamma_rates[i] := fnDiscretizeGamma( alpha[i], alpha[i], 4, false )

    # add moves for the alpha parameter
    moves[++mvi] = mvScale(alpha[i],weight=2)

    # the probability of a site being invariable
    pinvar[i] ~ dnBeta(1,1)
    moves[++mvi] = mvScale(pinvar[i], lambda=0.1, tune=true, weight=2.0)
    moves[++mvi] = mvSlide(pinvar[i], delta=0.1, tune=true, weight=2.0)

}

##############
# Tree model #
##############

#Constraints on clades
clade_A = clade("Resomia_ornicephala", "Frillagalma_vityazi")
clade_B = clade("Cordagalma_ordinatum", "Forskalia_formosa", "Forskalia_asymmetrica", "Forskalia_edwardsii", "Forskalia_tholoides")
clade_C = clade("Athorybia_rosacea", "Agalma_clausi", "Agalma_elegans", "Agalma_okenii", "Nanomia_bijuga", "Halistemma_rubrum")
clade_D = clade("Lychnagalma_utricularia","Physophora_gilmeri", "Physophora_hydrostatica", clade_C)
clade_BD = clade( clade_B, clade_D )
clade_ABC = clade( clade_A, clade_B, clade_D )
clade_E = clade( clade_ABC, "Erenna_richardi", "Erenna_sirena", "Erenna_laciniata", "Stephalia_dilata", "Thermopalia_sp", "Marrus_claudanielis", "Physonect_sp", "Stephanomia_amphytridis" )

clade_constraints = [ clade_A, clade_B, clade_C, clade_D, clade_BD, clade_ABC, clade_E ]

topology ~ dnUniformTopology( taxa=taxa, constraints=clade_constraints )

# construct a variable for the tree drawn from a birth death process
#ctt = clade(clade("Resomia_ornicephala", "Frillagalma_vityazi"), clade(clade("Cordagalma_ordinatum", "Forskalia_formosa", "Forskalia_asymmetrica", "Forskalia_edwardsii", "Forskalia_tholoides"), clade("Lychnagalma_utricularia","Physophora_gilmeri", "Physophora_hydrostatica", "Athorybia_rosacea", "Agalma_clausi", "Agalma_elegans", "Agalma_okenii", "Nanomia_bijuga", "Halistemma_rubrum")))
#xxx = [clade(ctt, "Erenna_richardi", "Erenna_sirena", "Erenna_laciniata", "Stephalia_dilata", "Thermopalia_sp", "Marrus_claudanielis", "Physonect_sp", "Stephanomia_amphytridis")]
#topology ~ dnUniformTopology(taxa=taxa, constraints=xxx)

# add topology Metropolis-Hastings moves
moves[++mvi] = mvNNI(topology, weight=1.0)
moves[++mvi] = mvSPR(topology, weight=1.0)

# create branch length vector and add moves
for (i in 1:n_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves[++mvi] = mvScale(br_lens[i])
}

# add deterministic node to monitor tree length
TL := sum(br_lens)

# unite topology and branch length vector into phylogeny object
phylogeny := treeAssembly(topology, br_lens)

###################
# PhyloCTMC Model #
###################

part_rate_mult ~ dnDirichlet( rep(1.0, n_data_subsets) )
moves[++mvi] = mvSimplexElementScale(part_rate_mult, alpha=10, tune=true, weight=2) 

for (i in 1:n_data_subsets) {
    part_rate[i] := part_rate_mult[i] * n_data_subsets
}


for (i in 1:n_data_subsets) {
    phyloSeq[i] ~ dnPhyloCTMC(tree=phylogeny, Q=Q[i], branchRates=part_rate[i], siteRates=gamma_rates[i], pInv=pinvar[i], type="DNA")
    phyloSeq[i].clamp(data[i])
}


############
# Analysis #
############

mymodel = model(phylogeny)

# add monitors
monitors[++mni] = mnModel(filename="output/gene_ct.log",printgen=10)
monitors[++mni] = mnFile(phylogeny, filename="output/gene_ct.trees", printgen=100)
monitors[++mni] = mnScreen(alpha,printgen=100)


# run the analysis
mymcmc = mcmc(mymodel, moves, monitors)
mymcmc.burnin(10000,1000)
mymcmc.run(300000)

# summarize output
treetrace = readTreeTrace("output/gene_ct.trees")
treetrace.summarize()

map_tree = mapTree(treetrace,"output/gene_map_ct.tre")


# you may want to quit RevBayes now
q()